# init.csh
#
# to use the Fink hierarchy, put the following in your .cshrc:
#
#  source BASEPATH/bin/init.csh
#

#
# Fink - a package manager that downloads source and installs it
# Copyright (c) 2001 Christoph Pfisterer
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#

set path = ( BASEPATH/bin BASEPATH/sbin $path /usr/X11R6/bin )

if ( $?MANPATH) then
  setenv MANPATH BASEPATH/share/man:BASEPATH/man:${MANPATH}:/usr/X11R6/man
else
  setenv MANPATH BASEPATH/share/man:BASEPATH/man:/usr/local/share/man:/usr/local/man:/usr/share/man:/usr/X11R6/man
endif

if ( $?INFOPATH ) then
  setenv INFOPATH BASEPATH/share/info:BASEPATH/info:$INFOPATH
else
  setenv INFOPATH BASEPATH/share/info:BASEPATH/info:/usr/local/share/info:/usr/local/lib/info:/usr/local/info:/usr/share/info
endif

if ( $?PERL5LIB ) then
  setenv PERL5LIB BASEPATH/lib/perl5:$PERL5LIB
else
  setenv PERL5LIB BASEPATH/lib/perl5
endif

# setting this is not neccessary and results in performance degradation
#if ( $?DYLD_LIBRARY_PATH ) then
#  setenv DYLD_LIBRARY_PATH BASEPATH/lib:$DYLD_LIBRARY_PATH
#else
#  setenv DYLD_LIBRARY_PATH BASEPATH/lib
#endif

# read per-package scripts from BASEPATH/etc/profile.d
if ( -d BASEPATH/etc/profile.d ) then
  set nonomatch
  foreach i ( BASEPATH/etc/profile.d/*.csh )
    if ( -x $i ) then
      source $i
    endif
  end
  unset i nonomatch
endif

# eof
